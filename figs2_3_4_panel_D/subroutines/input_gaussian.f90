!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!                                                                            !!!!!!!!
!!!!!!!!        CORRELATED INPUT FOLLOWING ORNSTEIN-UHLENBECK PROCESS               !!!!!!!!
!!!!!!!!        FROM OU PROCESS TO SPIKES (INHOMOGENEOUS POISSON PROCESS)           !!!!!!!!
!!!!!!!!                                                                            !!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	SUBROUTINE input_g(t)
	USE PARAMETERS
	USE VARIABLES
	USE TEMP
	IMPLICIT NONE
	INTEGER			::	pw,t,j0,jf
	REAL*8 			::	randg

	spkr = 0.0d0
!======================= GENERATING CORRELATIONS IN GROUPS OF NEURONS =====================!
	IF(MOD(t,10).EQ.0) THEN		!update every 10 time steps - 1 ms
		DO pw = 1,n_pw
		!------------------------gaussian random number--------------------------------------------!
			CALL grand(1.0d0,randg)
		!---------------------update in OU variable------------------------------------------------!
			patt_time(pw) = patt_time(pw)*pr(80) + pr(82)*randg*pr(81)
		!============================== SPIKE PROBABILITIES =======================================!
			IF(patt_time(pw).GT.0.0d0) THEN		!rectifying OU variable
			!---------------------------excitatory neurons---------------------------------------------!
				inp_patt(((pw-1)*ne_pw)+1:ne_pw*pw) = (dt*patt_time(pw))/1000.0d0
			!---------------------------inhibitory neurons pop 1---------------------------------------!
				inp_patt(((pw-1)*ni_pw1)+ne_input+1:ne_input+ni_pw1*pw) = 2.0d0*(dt*patt_time(pw))/1000.0d0
			!---------------------------inhibitory neurons pop 2---------------------------------------!
				inp_patt(((pw-1)*ni_pw2)+ne_input+ni_input1+1:ne_input+ni_input1+ni_pw2*pw) = 2.0d0*(dt*patt_time(pw))/1000.0d0
			ELSE					!rectifying OU variable
			!---------------------------excitatory neurons---------------------------------------------!
				inp_patt(((pw-1)*ne_pw)+1:ne_pw*pw) = 0.0d0
			!---------------------------inhibitory neurons pop 1---------------------------------------!
				inp_patt(((pw-1)*ni_pw1)+ne_input+1:ne_input+ni_pw1*pw) = 0.0d0
			!---------------------------inhibitory neurons pop 2---------------------------------------!
				inp_patt(((pw-1)*ni_pw2)+ne_input+ni_input1+1:ne_input+ni_input1+ni_pw2*pw) = 0.0d0
			END IF
		!==========================================================================================!
		END DO
	END IF
!==========================================================================================!
!======================== GENERATE SPIKES FROM RATE PROPABILITIES =========================!
	spk = .FALSE.
	CALL RANDOM_NUMBER(tmp_inp)
!-------------------------excitatory neurons-----------------------------------------------!
	j0 = 1
	jf = ne_input
	WHERE((tmp_inp(j0:jf).LE.(pr(75)+inp_patt(j0:jf))).AND.(tr-spkt(j0:jf).GT.pr(3)))
		spkr(j0:jf) = 1.0d0
		spkt(j0:jf) = tr
	END WHERE
!------------------------------------------------------------------------------------------!
!-------------------------inhibitory neurons 1---------------------------------------------!
	j0 = ne_input + 1
	jf = ne_input + ni_input1
	WHERE((tmp_inp(j0:jf).LE.(pr(76)+pr(205)*inp_patt(j0:jf))).AND.(tr-spkt(j0:jf).GT.(0.5d0*pr(3))))
		spkr(j0:jf) = 1.0d0
		spkt(j0:jf) = tr
	END WHERE
!------------------------------------------------------------------------------------------!
!-------------------------inhibitory neurons 2---------------------------------------------!
	j0 = ne_input + ni_input1 + 1
	jf = ne_input + ni_input1 + ni_input2
	WHERE((tmp_inp(j0:jf).LE.(pr(76)+pr(206)*inp_patt(j0:jf))).AND.(tr-spkt(j0:jf).GT.(0.5d0*pr(3))))
		spkr(j0:jf) = 1.0d0
		spkt(j0:jf) = tr
	END WHERE
!------------------------------------------------------------------------------------------!
!==========================================================================================!
!=========================== FROM SPIKES TO CONDUCTANCES ==================================!
!-------------------------excitatory neurons-----------------------------------------------!
	x(3) = x(3) + SUM(spkr(1:ne_input)*msynw(1:ne_input))
!------------------------------------------------------------------------------------------!
!-------------------------inhibitory neurons-----------------------------------------------!
	x(4) = x(4) + SUM(spkr(ne_input+1:ne_input+ni_input1+ni_input2)*msynw(ne_input+1:ne_input+ni_input1+ni_input2))
!------------------------------------------------------------------------------------------!
!==========================================================================================!
!============================= UPDATE OF VARIABLE FOR PLOT ================================!
	DO pw = 1,n_pw
		activity(pw) = activity(pw)*pr(101) + SUM(spkr(((pw-1)*ne_pw)+1:pw*ne_pw))*pr(102)
	END DO
!==========================================================================================!
!==========================================================================================!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!                                                                            !!!!!!!!
!!!!!!!!             PSEUDO RANDOM GAUSSIAN RANDOM NUMBER GENERATOR                 !!!!!!!!
!!!!!!!!                                                                            !!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	END SUBROUTINE
	SUBROUTINE grand(std_dev,randg)
	USE PARAMETERS
	IMPLICIT NONE
	REAL*8 :: random_number_g(2),std_dev
	REAL*8 :: randg

	CALL RANDOM_NUMBER(random_number_g)
	randg = std_dev*DSQRT( -2.0d0*DLOG(random_number_g(1)) )*DCOS( dpi*random_number_g(2) )

	END SUBROUTINE
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
